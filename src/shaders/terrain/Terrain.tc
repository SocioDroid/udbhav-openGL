#version 460 core

layout(vertices=4) out;

uniform mat4 u_modelMatrix;
uniform mat4 u_viewMatrix;

in vec2 a_texcoord_out[];

in vec3 FragPos[];
in vec3 TangentLightPos[];
in vec3 TangentViewPos[];
in vec3 TangentFragPos[];

out vec2 a_texcoord_tcs_out[];
out vec3 FragPos_tcs_out[];
out vec3 TangentLightPos_tcs_out[];
out vec3 TangentViewPos_tcs_out[];
out vec3 TangentFragPos_tcs_out[];


void main()
{
    gl_out[gl_InvocationID].gl_Position = gl_in[gl_InvocationID].gl_Position;
    a_texcoord_tcs_out[gl_InvocationID] = a_texcoord_out[gl_InvocationID];
    
    FragPos_tcs_out[gl_InvocationID] = FragPos[gl_InvocationID];
    TangentLightPos_tcs_out[gl_InvocationID] = TangentLightPos[gl_InvocationID];
    TangentViewPos_tcs_out[gl_InvocationID] = TangentViewPos[gl_InvocationID];
    TangentFragPos_tcs_out[gl_InvocationID] = TangentFragPos[gl_InvocationID];

    if(gl_InvocationID == 0)
    {

        const int MIN_TESS_LEVEL = 4;
        const int MAX_TESS_LEVEL = 64;
        const float MIN_DISTANCE = 20;
        const float MAX_DISTANCE = 800;

        vec4 eyeSpacePos00 = u_viewMatrix * u_modelMatrix * gl_in[0].gl_Position;
        vec4 eyeSpacePos01 = u_viewMatrix * u_modelMatrix * gl_in[1].gl_Position;
        vec4 eyeSpacePos10 = u_viewMatrix * u_modelMatrix * gl_in[2].gl_Position;
        vec4 eyeSpacePos11 = u_viewMatrix * u_modelMatrix * gl_in[3].gl_Position;

        // "distance" from camera scaled between 0 and 1
        float distance00 = clamp( (abs(eyeSpacePos00.z) - MIN_DISTANCE) / (MAX_DISTANCE-MIN_DISTANCE), 0.0, 1.0 );
        float distance01 = clamp( (abs(eyeSpacePos01.z) - MIN_DISTANCE) / (MAX_DISTANCE-MIN_DISTANCE), 0.0, 1.0 );
        float distance10 = clamp( (abs(eyeSpacePos10.z) - MIN_DISTANCE) / (MAX_DISTANCE-MIN_DISTANCE), 0.0, 1.0 );
        float distance11 = clamp( (abs(eyeSpacePos11.z) - MIN_DISTANCE) / (MAX_DISTANCE-MIN_DISTANCE), 0.0, 1.0 );

        float tessLevel0 = mix( MAX_TESS_LEVEL, MIN_TESS_LEVEL, min(distance10, distance00) );
        float tessLevel1 = mix( MAX_TESS_LEVEL, MIN_TESS_LEVEL, min(distance00, distance01) );
        float tessLevel2 = mix( MAX_TESS_LEVEL, MIN_TESS_LEVEL, min(distance01, distance11) );
        float tessLevel3 = mix( MAX_TESS_LEVEL, MIN_TESS_LEVEL, min(distance11, distance10) );

        gl_TessLevelOuter[0] = tessLevel0;
        gl_TessLevelOuter[1] = tessLevel1;
        gl_TessLevelOuter[2] = tessLevel2;
        gl_TessLevelOuter[3] = tessLevel3;

        gl_TessLevelInner[0] = max(tessLevel1, tessLevel3);
        gl_TessLevelInner[1] = max(tessLevel0, tessLevel2);
    }
}
